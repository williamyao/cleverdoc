;---------------------------------------------------------------------
;  TESTING OPERATIONS

(in-package #:cleverdoc)

(define-op ==> (left right)
  `((,*fn* ,@left) === ,@right))

(define-op ==/ (left right)
  `((,*fn* ,@left) =/= ,@right))

;; (define-op =>> (left right)
;;   (with-gensyms (out)
;;     `(let ((,out (make-in-memory-output-stream
;;                   :element-type ,(typecase (car right)
;;                                    (string 'character)
;;                                    (t 'octet)))))
;;        (,*fn* ,@left)
;;        ((get-output-stream-sequence ,out) === ,@right))))

(define-op === (left right)
  (unless (= (length left) 1)
    (error "Left-side argument to === should be a single form"))
  `(handler-case
       (if (equal (subseq (multiple-value-list ,@left)
                          0 ,(length right))
                  (list ,@right))
           (pass)
           (fail ,(format nil "狺蒋浸狺戾骠蜷玷舂┅ㄥ蝌矧ㄥㄦ衢ㄦ矧磲铋⑴钽秕铘弪邃弪蝻轭浇浸狺К漓彐К蜷玷舂┅┅ㄤ彐轭瀛镳蒋戾骠蜷玷舂躅戾篌戾铉翳戾骠暴ㄥ蝌矧⑻彐舡箝溴狎珲礤铘麸蒋箬秕熹忮箝铉戾骘蝽┅啜栳钿戾颦汜箦ㄩ铒ㄥ聃犰篚怏羼眭祠轲戾鲠祯瀛扉篝漓彐舂戾铉翳蜷玷舂扉篝莉殓梏┅疳篌ㄦ衢ㄦ矧磲铋狺浇浸狺戾骠蜷玷舂┅ㄥ蝌矧ㄥㄦ衢ㄦ矧磲铋⑴钽秕铘弪邃弪蝻轭蒋浸狺К漓彐К蜷玷舂┅┅ㄤ彐轭瀛镳浇戾骠蜷玷舂啜ì骖漓彐舂进莉殓梏┅ㄤ彐轭瀛镳进戾骠蜷玷舂躅戾篌戾铉翳戾骠暴ㄥ蝌矧⑻彐舡箝溴狎珲礤铘麸浇箬秕熹忮箝铉戾骘蝽┅啜栳钿戾颦汜箦漓彐括磲疸狎灬礅溽ㄥ啜ī疳篌┅蜷玷舂ê铒弪蝻é蝈篝蝈篝ㄤ邈灬蝈ㄩ珙矧蝈篝┅ㄦ衢ㄦ矧磲铋⑽弪蝻孱泔躅翦蝈轭岙呆揍铘邃镱镦狺ㄣ狎戾骠蜷玷舂┅┅